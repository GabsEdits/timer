# Maintainer: vikdevelop <super-vik1@protonmail.com>

pkgname=timer
_appid="com.github.vikdevelop.$pkgname"
pkgver=1.6
pkgrel=1
pkgdesc="Simple Timer in PyGTK"
arch=('any')
url="https://github.com/vikdevelop/timer"
license=('GPL3')
depends=('python-gobject')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('c52663542c49fa3b57331d660c67c5922c3ca32e2fa91d5b0b874dc6d2e9c7eb')

prepare() {
  cd "$pkgname-$pkgver"

  # Exec Python script directly and rename to appid
  sed -i "s|$pkgname.sh|$_appid|g" "flatpak/$_appid.desktop"

  # Add shebang to Python script
  sed -i '1 i\#!/usr/bin/env python' "src/$pkgname.py"
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "src/$pkgname.py" "$pkgdir/usr/bin/$_appid"
  install -Dm644 "flatpak/$_appid.desktop" -t "$pkgdir/usr/share/applications/"
  install -Dm644 "flatpak/icons/$_appid.png" -t "$pkgdir/usr/share/pixmaps"
  install -Dm644 "flatpak/$_appid.metainfo.xml" -t "$pkgdir/usr/share/metainfo"
}
